subroutine iniparam
  
  !  Initialization of parameter arrays
  !  INPUT : ---
  !  OUTPUT: PARAM          List of all parameters
  !          NOUTPARAM      Number of output parameters
  !          IOUTPARAM      Addresses of output parameters
  !          NPARAMTOT      Number of all parameters
  
  use netcdf
  use chimere_consts
  use chimere_common
  use wholedomain_common
  implicit none
  
  
#define NCERR(lnum) if(ncstat/=NF90_NOERR) call nc_err(ncstat,lnum,'iniparam.f90')

  !****** parameters
  !  Constant netCDF attributes
  character(len=*),parameter :: title=      'CHIMERE SUITE'
  character(len=*),parameter :: subtitle=   'Hourly output parameters file'
  character(len=*),parameter :: generator=  'Generated by chimere'
  character(len=*),parameter :: conventions='None'
  
  !*****************************************************************************************
  ! Local variables
  ! arrays
  real(kind=8),dimension(nzonal_domain,nmerid_domain) :: buf2d
  ! strings
  character(len=1024) :: history
  ! scalars
  integer :: ioutpar
  integer :: ncstat
  integer :: par_time_dimid
  integer :: par_date_dimid
  integer :: par_zonal_dimid
  integer :: par_merid_dimid
  
  ! System information stuff
  character(len=32) :: systime
  character(len=64) :: hname
  character(len=32) :: usrname
  character(len=255) :: cwd
  
  !*****************************************************************************************
  
  ! get various system informations
  call get_system(usrname,hname,systime,cwd)
  
  ! create "par" file
  ncstat=nf90_create(fnothers,NF90_CLOBBER,par_ncid)
  NCERR(__LINE__)
  
  ! create dimensions in "par" file
  call create_dims_par
  
  ! write attributes to "par" file
  call create_atts_par
  
  ! select fields to store
  call select_fields
  
  ! create the corresponding variables
  call create_vars_par
  
  ! end define mode
  ncstat=nf90_enddef(par_ncid)
  NCERR(__LINE__)
  
  ! record longitude and latitude
  buf2d=xlong
  ncstat=nf90_put_var(par_ncid,par_lon_varid,buf2d)
  NCERR(__LINE__)
  buf2d=xlati
  ncstat=nf90_put_var(par_ncid,par_lat_varid,buf2d)
  NCERR(__LINE__)


contains
  
  !****************************************************
  subroutine create_dims_par
    implicit none
    
    ncstat=nf90_def_dim(par_ncid,'Time',        NF90_UNLIMITED,par_time_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(par_ncid,'DateStrLen',  dlen,          par_date_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(par_ncid,'west_east',   nzonal_domain,        par_zonal_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(par_ncid,'south_north', nmerid_domain,        par_merid_dimid)
NCERR(__LINE__)
  end subroutine create_dims_par
  
  
  !****************************************************
  subroutine create_atts_par
    implicit none
    
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Title',title)
NCERR(__LINE__)
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Sub-title',subtitle)
NCERR(__LINE__)
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Chimere_type','par')
NCERR(__LINE__)
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Generating_process',generator)
NCERR(__LINE__)
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Conventions',conventions)
NCERR(__LINE__)
    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'Domain',domain)
NCERR(__LINE__)
    
#if defined(IFORT)

    history = &
            'File '//fnothers(1:len_trim(fnothers))   // &
                    ' was generated on '                      // &
                    systime(1:len_trim(systime))              // &
                    ' by '                                    // &
                    usrname(1:len_trim(usrname))              // &
                    ' on '                                    // &
                    hname(1:len_trim(hname))                  //'\n'C
    history = history(1:len_trim(history)-1)
    
#elif (defined(G95)+defined(PGI))

    history = &
            'File '//fnothers(1:len_trim(fnothers))   // &
                    ' was generated on '                      // &
                    systime(1:len_trim(systime))              // &
                    ' by '                                    // &
                    usrname(1:len_trim(usrname))              // &
                    ' on '                                    // &
                    hname(1:len_trim(hname))
    history = history(1:len_trim(history)-1)
    
#else

    history='unknown'
    
#endif

    ncstat=nf90_put_att(par_ncid,NF90_GLOBAL,'history',history(1:len_trim(history)))
NCERR(__LINE__)
  
  end subroutine create_atts_par
  
  !****************************************************
  subroutine select_fields
  
#define DO_IT(string) \
    ioutpar=ioutpar+1 ;\
  output_par(ioutpar)%name=string ;\
  output_par(ioutpar)%printit=.true.
  
  
  do ioutpar=1,nparammax
    output_par(ioutpar)%printit=.false.
  end do
  
  ioutpar=0
  
  ! wind
  DO_IT('winhs')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Lower layer horizontal wind'
  
  DO_IT('winh2')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Second layer horizontal wind'
  
  DO_IT('winvs')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Lower layer vertical wind'
  
  DO_IT('winvt')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Top layer vertical wind'
  
  ! specific humidity, air density, temperature, vertical velocity
  DO_IT('sphu')
  output_par(ioutpar)%units='Kg/Kg'
  output_par(ioutpar)%long_name='Specific humidity in lower layer'
  
  DO_IT('airm')
  output_par(ioutpar)%units='molecule/cm**3'
  output_par(ioutpar)%long_name='Air density in lower layer'
  
  DO_IT('temp')
  output_par(ioutpar)%units='degC'
  output_par(ioutpar)%long_name='Air temperature in lower layer'
  
  DO_IT('winxs')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Velocity-equivalent of mixing exchange rate in lower layer'
  
  DO_IT('winxt')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Velocity-equivalent of mixing exchange rate in top layer'
  
  
  ! model layer depths
  DO_IT('thlay')
  output_par(ioutpar)%units='m'
  output_par(ioutpar)%long_name='Lower layer thickness'
  
  ! mixing height, radiation
  DO_IT('hght')
  output_par(ioutpar)%units='m'
  output_par(ioutpar)%long_name='Boundary layer height'
  
  DO_IT('atte')
  output_par(ioutpar)%units='0-1'
  output_par(ioutpar)%long_name='Radiation attenuation factor'
  
  DO_IT('zeni')
  output_par(ioutpar)%units='0-1'
  output_par(ioutpar)%long_name='Cosine of zenith angle'
  
  DO_IT('tem2')
  output_par(ioutpar)%units='degC'
  output_par(ioutpar)%long_name='2m air temperature'
  
  
  ! diagnostic turbulent parameters
  DO_IT('usta')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Friction velocity'
  
  DO_IT('aerr')
  output_par(ioutpar)%units='s/m'
  output_par(ioutpar)%long_name='Aerodynamic resistance'
  
  !    DO_IT('obuk')
  !    output_par(ioutpar)%units='m'
  !    output_par(ioutpar)%long_name='Obukov length'
  
  DO_IT('wsta')
  output_par(ioutpar)%units='m/s'
  output_par(ioutpar)%long_name='Convective velocity'
  
  DO_IT('depo')
  output_par(ioutpar)%units='cm/s'
  output_par(ioutpar)%long_name='Deposition rate'
  
  
  ! precipitation and water parameter
  
  DO_IT('topc')
  output_par(ioutpar)%units='mm/hour'
  output_par(ioutpar)%long_name='Total precipitation'
  
  DO_IT('clwc')
  output_par(ioutpar)%units='kg/Kg'
  output_par(ioutpar)%long_name='Cloud liquid water + ice content'
  
  DO_IT('humi')
  output_par(ioutpar)%units='%'
  output_par(ioutpar)%long_name='Relative humidity'
  
  
  end subroutine select_fields
  
  
  !****************************************************
  subroutine create_vars_par
    implicit none
    
    ! LON
    ncstat=nf90_def_var(                            &
            par_ncid,                                  &
            'lon',                                     &
            NF90_DOUBLE,                                &
            (/par_zonal_dimid,par_merid_dimid/),       &
            par_lon_varid                              &
            )
NCERR(__LINE__)
    ncstat=nf90_put_att(                            &
            par_ncid,                                  &
            par_lon_varid,                             &
            'units',                                   &
            'degrees_east'                             &
            )
NCERR(__LINE__)
    ncstat=nf90_put_att(                            &
            par_ncid,                                  &
            par_lon_varid,                             &
            'long_name',                               &
            'Longitude'                                &
            )
NCERR(__LINE__)
    
    ! LAT
    ncstat=nf90_def_var( &
            par_ncid, &
            'lat', &
            NF90_DOUBLE, &
            (/par_zonal_dimid,par_merid_dimid/),       &
            par_lat_varid                              &
            )
NCERR(__LINE__)
    ncstat=nf90_put_att(                            &
            par_ncid,                                  &
            par_lat_varid,                             &
            'units',                                   &
            'degrees_north'                            &
            )
NCERR(__LINE__)
    ncstat=nf90_put_att(                            &
            par_ncid,                                  &
            par_lat_varid,                             &
            'long_name',                               &
            'Latitude'                                 &
            )
NCERR(__LINE__)
    
    ! TIMES
    ncstat=nf90_def_var(                            &
            par_ncid,                                  &
            'Times',                                   &
            NF90_CHAR,                                 &
            (/par_date_dimid,par_time_dimid/),         &
            par_times_varid                            &
            )
NCERR(__LINE__)
    
    ! METEO FIELDS
    do ioutpar=1,nparammax
      if (output_par(ioutpar)%printit) then
        if (NetCDF_outtype.eq.1)then
          ncstat=nf90_def_var(                      &
                  par_ncid,                            &
                  output_par(ioutpar)%name,            &
                  NF90_DOUBLE,                          &
                  (/par_zonal_dimid,par_merid_dimid,par_time_dimid/), &
                  output_par(ioutpar)%varid            &
                  )
        else
          ncstat=nf90_def_var(                      &
                  par_ncid,                            &
                  output_par(ioutpar)%name,            &
                  NF90_FLOAT,                          &
                  (/par_zonal_dimid,par_merid_dimid,par_time_dimid/), &
                  output_par(ioutpar)%varid            &
                  )
        end if
        NCERR(__LINE__)
        ncstat=nf90_put_att(                      &
                par_ncid,                            &
                output_par(ioutpar)%varid,           &
                'units',                             &
                output_par(ioutpar)%units            &
                )
        NCERR(__LINE__)
        ncstat=nf90_put_att(                      &
                par_ncid,                            &
                output_par(ioutpar)%varid,           &
                'long_name',                         &
                output_par(ioutpar)%long_name        &
                )
        NCERR(__LINE__)
      end if
    end do
  
  
  end subroutine create_vars_par

end subroutine iniparam
