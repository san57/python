subroutine inidepout

  !  Initialization of deposition file

  use netcdf
  use chimere_consts
  use chimere_common
  use wholedomain_common
  implicit none

#define NCERR(lnum) if(ncstat/=NF90_NOERR) call nc_err(ncstat,lnum,'inidepout.f90')

  !****** parameters
  integer,parameter :: spnamelen = 20

  !  Constant netCDF attributes
  character(len=*),parameter :: title=      'CHIMERE SUITE'
  character(len=*),parameter :: subtitle=   'Final deposition file'
  character(len=*),parameter :: generator=  'Generated by chimere'
  character(len=*),parameter :: conventions='None'

  !*****************************************************************************************
  ! Local variables
  ! arrays
  real(kind=8),dimension(nzonal_domain,nmerid_domain) :: buf2d
  character(len=spnamelen),dimension(2*nspec) :: bufspec
  ! strings
  character(len=1024) :: history
  ! scalars
  integer :: is
  integer :: ncstat
  integer :: depo_time_dimid
  integer :: depo_date_dimid
  integer :: depo_splen_dimid
  integer :: depo_species_dimid
  integer :: depo_zonal_dimid
  integer :: depo_merid_dimid

  ! System information stuff
  character(len=32) :: systime
  character(len=64) :: hname
  character(len=32) :: usrname
  character(len=255) :: cwd

  !*****************************************************************************************

  ! get various system informations
  call get_system(usrname,hname,systime,cwd)

  ! create "depo" file
  ncstat=nf90_create(fndepos,NF90_CLOBBER,depo_ncid)
  NCERR(__LINE__)

  ! create dimensions in "depo" file
  call create_dims_depo

  ! write attributes to "depo" file
  call create_atts_depo

  ! scatter the "depo" arrays into separate variables
  call create_vars_depo

  ! end define mode
  ncstat=nf90_enddef(depo_ncid)
  NCERR(__LINE__)

  ! record longitude and latitude
  buf2d=xlong
  ncstat=nf90_put_var(depo_ncid,depo_lon_varid,buf2d)
  NCERR(__LINE__)
  buf2d=xlati
  ncstat=nf90_put_var(depo_ncid,depo_lat_varid,buf2d)
  NCERR(__LINE__)


  ! record species
  do is=1,2*nspec
     bufspec(is)=output_depo(is)%name
     !print *,bufspec(is)
  end do
  ncstat=nf90_put_var(depo_ncid,depo_species_varid,bufspec)
  NCERR(__LINE__)

  ! Synchronize disk to avoid data loss
  ncstat=nf90_sync(depo_ncid)
  NCERR(__LINE__)

 contains

  !****************************************************
  subroutine create_dims_depo
    implicit none

    ncstat=nf90_def_dim(depo_ncid,'Time',      NF90_UNLIMITED,  depo_time_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(depo_ncid,'DateStrLen',          dlen,  depo_date_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(depo_ncid,'SpStrLen',     spnamelen,  depo_splen_dimid)
NCERR(__LINE__)

    ncstat=nf90_def_dim(depo_ncid,'Species',          2*nspec,  depo_species_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(depo_ncid,'west_east',         nzonal_domain,  depo_zonal_dimid)
NCERR(__LINE__)
    ncstat=nf90_def_dim(depo_ncid,'south_north',       nmerid_domain,  depo_merid_dimid)
NCERR(__LINE__)
  end subroutine create_dims_depo


  !****************************************************
  subroutine create_atts_depo
    implicit none

    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Title',title)
NCERR(__LINE__)
    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Sub-title',subtitle)
NCERR(__LINE__)
    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Chimere_type','dep')
NCERR(__LINE__)
    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Generating_process',generator)
NCERR(__LINE__)
    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Conventions',conventions)
NCERR(__LINE__)
    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'Domain',domain)
NCERR(__LINE__)

#if defined(IFORT)

    history =                                                                    &
         'File '//fndepos(1:len_trim(fndepos))                                // &
         ' was generated on '                                                 // &
         systime(1:len_trim(systime))                                         // &
         ' by '                                                               // &
         usrname(1:len_trim(usrname))                                         // &
         ' on '                                                               // &
         hname(1:len_trim(hname))                                             //'\n'C
    history = history(1:len_trim(history)-1)

#elif (defined(G95)+defined(PGI))

    history =                                                                    &
         'File '//fndepos(1:len_trim(fndepos))                                // &
         ' was generated on '                                                 // &
         systime(1:len_trim(systime))                                         // &
         ' by '                                                               // &
         usrname(1:len_trim(usrname))                                         // &
         ' on '                                                               // &
         hname(1:len_trim(hname))
    history = history(1:len_trim(history)-1)

#else

    history='unknown'

#endif

    ncstat=nf90_put_att(depo_ncid,NF90_GLOBAL,'history',history(1:len_trim(history)))
NCERR(__LINE__)

  end subroutine create_atts_depo

  !****************************************************
  subroutine create_vars_depo
    implicit none

    ! LON
    ncstat=nf90_def_var(                                                         &
         depo_ncid,                                                              &
         'lon',                                                                  &
         NF90_FLOAT,                                                             &
         (/depo_zonal_dimid,depo_merid_dimid/),                                  &
         depo_lon_varid                                                          &
         )
NCERR(__LINE__)
    ncstat=nf90_put_att(                                                         &
         depo_ncid,                                                              &
         depo_lon_varid,                                                         &
         'units',                                                                &
         'degrees_east'                                                          &
         )
NCERR(__LINE__)
    ncstat=nf90_put_att(                                                         &
         depo_ncid,                                                              &
         depo_lon_varid,                                                         &
         'long_name',                                                            &
         'Longitude'                                                             &
         )
NCERR(__LINE__)

    ! LAT
    ncstat=nf90_def_var( &
         depo_ncid, &
         'lat', &
         NF90_FLOAT, &
         (/depo_zonal_dimid,depo_merid_dimid/),                                  &
         depo_lat_varid                                                          &
         )
NCERR(__LINE__)
    ncstat=nf90_put_att(                                                         &
         depo_ncid,                                                              &
         depo_lat_varid,                                                         &
         'units',                                                                &
         'degrees_north'                                                         &
         )
NCERR(__LINE__)
    ncstat=nf90_put_att(                                                         &
         depo_ncid,                                                              &
         depo_lat_varid,                                                         &
         'long_name',                                                            &
         'Latitude'                                                              &
         )
NCERR(__LINE__)

    ! SPECIES
    ncstat=nf90_def_var( &
         depo_ncid,      &
         'species', &
         NF90_CHAR,      &
         (/depo_splen_dimid,depo_species_dimid/),                                &
         depo_species_varid                                                      &
         )
NCERR(__LINE__)

    ! TIMES
    ncstat=nf90_def_var(                                                         &
         depo_ncid,                                                              &
         'Times',                                                                &
         NF90_CHAR,                                                              &
         (/depo_date_dimid,depo_time_dimid/),                                    &
         depo_times_varid                                                        &
         )
NCERR(__LINE__)

    ! DEPOSITION FIELDS
    ! fill table
    do is=1,nspec
       output_depo(is)%name      =species(is)%name(1:len_trim(species(is)%name))//'_DRY'
       output_depo(is+nspec)%name=species(is)%name(1:len_trim(species(is)%name))//'_WET'
    end do


    ! create variables
    do is=1,2*nspec
       ncstat=nf90_def_var(                                                      &
            depo_ncid,                                                           &
            output_depo(is)%name,                                                &
            NF90_FLOAT,                                                          &
            (/depo_zonal_dimid,depo_merid_dimid,depo_time_dimid/), &
            output_depo(is)%varid                                                &
            )
       NCERR(__LINE__)
       ncstat=nf90_put_att(                                                      &
            depo_ncid,                                                           &
            output_depo(is)%varid,                                               &
            'units',                                                             &
            'molecules/cm2'                                                      &
            )
       NCERR(__LINE__)
    end do


  end subroutine create_vars_depo

end subroutine inidepout
